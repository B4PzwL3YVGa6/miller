begin {
  if (isabsent(@olen)) {
    @olen = 16;
  }
  if (isabsent(@ocount)) {
    @ocount = 16;
  }
}

for (_, v in $*) {
  local n = strlen(v);
  if (n >= 2) {
    local a = substr(v, 0, 0);
    @a_histo[a] += 1;

    local b = substr(v, 1, 1);
    @ab_histo[a][b] += 1;

    for (local i = 0; i < n-2; i += 1) {
      local a = substr(v, i, i);
      local b = substr(v, i+1, i+1);
      local c = substr(v, i+2, i+2);
      @abc_histo[a][b][c] += 1;
    }
  }
}

end {

  local a_sum = 0;
  for (a, na in @a_histo) {
    a_sum += na;
  }
  local a_cumu = 0.0;
  for (a, _ in @a_histo) {
    local a_p = @a_histo[a] / a_sum;
    a_cumu += a_p;
    @a_pmf[a] = a_p;
    @a_cmf[a] += a_cumu;
  }

  for (a in @ab_histo) {

    local ab_sum = 0.0;
    for (b, nab in @ab_histo[a]) {
      ab_sum += nab;
    }
    local ab_cumu = 0.0;
    for (b, _ in @ab_histo[a]) {
      local ab_p = @ab_histo[a][b] / ab_sum;
      ab_cumu += ab_p;
      @ab_pmf[a][b] = ab_p;
      @ab_cmf[a][b] = ab_cumu;
    }
  }

  for (a in @abc_histo) {
    for (b in @abc_histo[a]) {

      local abc_sum = 0.0;
      for (c, nabc in @abc_histo[a][b]) {
        abc_sum += nabc;
      }
      local abc_cumu = 0.0;
      for (c, _ in @abc_histo[a][b]) {
        local abc_p = @abc_histo[a][b][c] / abc_sum;
        abc_cumu += abc_p;
        @abc_pmf[a][b][c] = abc_p;
        @abc_cmf[a][b][c] = abc_cumu;
      }
    }
  }

  #dump;

  for (local oi = 0; oi < @ocount; oi += 1) {
    oa_u = urand();
    oa = "?";
    for (a, c in @a_cmf) {
      oa = a;
      if (oa_u < c) {
        break;
      }
    }
    local out = oa;

    ob_u = urand();
    ob = "?";
    for (b, c in @ab_cmf[oa]) {
      ob = b;
      if (ob_u < c) {
        break;
      }
    }
    out .= ob;

    for (local i = 2; i < @olen; i += 1) {
      local abc_u = urand();
      local oc = "???";
      #print;
      #print "i=".i;
      #print "oa = ".oa." ob = ".ob;
      for (c, d in @abc_cmf[oa][ob]) {
        #print "d = ".d;
        oc = c;
        if (abc_u < d) {
          break;
        }
      }
      if (oc == "???") {
        break;
      }
      #print "i=".i.",oa=".oa.",ob=".ob.",oc=".oc;
      #print "oc = ".oc;
      out .= oc;
      oa = ob;
      ob = oc;
    }
    print out;

  }
}
